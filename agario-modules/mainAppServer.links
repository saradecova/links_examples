open Util.Lists
open Render
open Logic
open MainAppClient
open ServerLogic

# keeps truck of all players, their states and channel on which the server
# communicates with them
fun players_manager(players, new_id) {
  receive {
    case Update(player_id, player, pid) ->
      var new_players = dictUpdate(player_id, player, players);
      pid ! new_players;
      players_manager(new_players, new_id)
    case Add(player_id:Int, player:PlayerState) ->
      players_manager(((player_id, player))::players, new_id)
    case GeneratePlayerId(pid) ->
      pid ! new_id;
      players_manager(players, new_id+1)
    case DeletePlayer(player_id) ->
      var new_players = filter(fun(x) {x.1<>player_id}, players);
      players_manager(new_players, new_id);
  }
}

fun handle_pings(channel:UpdatePlayerChannel, manager) {
  offer(channel) {
    case Update(channel) ->
      var (x, channel) = receive(channel);
      var player_id = x.1;
      var player_state:PlayerState = x.2;
      var players = spawnWait { manager ! Update(player_id, player_state, self());
                                receive { case x -> x }};
      handle_pings(send(players, channel), manager);
      ()
    case DeletePlayer(channel) ->
      var (player_id, channel) = receive(channel);
      manager ! DeletePlayer(player_id);
      ()
  }
}

fun setup(s:InitiateGame, manager) {
  # send new player id.
  var player_id = spawnWait { manager ! GeneratePlayerId(self());
                              receive { case n -> n }};
  var s = send(player_id, s);

  #  Get client's initial state. (is same for every client so should be
  #  generated at the server. TODO: rewrite initial_game_state funtion so it
  #  does not use pure client-side functions and can bu run on server).
  var (player_state:PlayerState, s) = receive(s);
  manager ! Add(player_id, player_state);
  var _ = spawn { handle_pings(s, manager) };
  ()
}

fun client_acceptor(ap, manager) {
  var s = accept(ap);
  var _ = spawn { setup(s, manager) };
  client_acceptor(ap, manager)
}

fun main() {
  var clientAP = new();

  var gameManager = spawn { players_manager([], 1) };
  var acc = spawn { client_acceptor(clientAP, gameManager) };
  var server_time = serverTime();
  addRoute("/", fun (_, _) { MainAppClient.mainPage(clientAP) } );
  addStaticRoute("/css", "css", [("css", "text/css")]);
  serveWebsockets();
  servePages()
}

main()
